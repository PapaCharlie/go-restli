import com.linkedin.pegasus.generator.PegasusDataTemplateGenerator


buildscript {
  dependencies {
    classpath group: "com.linkedin.pegasus", name: "restli-tools", version: pegasusVersion
  }
}

plugins {
  id "java-library"
  id "idea"
  id "maven-publish"
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

dependencies {
  implementation group: "com.linkedin.pegasus", name: "restli-tools", version: pegasusVersion
  implementation group: "com.google.code.gson", name: "gson", version: "2.8.5"
  implementation group: "com.google.guava", name: "guava", version: "28.1-jre"

  testImplementation group: "org.testng", name: "testng", version: "6.14.3"
}

test {
  useTestNG()
}

File generatedRestliCode = file("src/mainGenerated/java")
task generateRestliBindings {
  // I couldn't for the life of me figure out how to import and run the "pegasus" plugin, so we're going to stick to
  // calling the generator manually for now
  doLast {
    String inputPath = file("src/main/pegasus").absolutePath
    String outputPath = generatedRestliCode.absolutePath
    String defaultPackage = "io.papacharlie.gorestli"
    String[] sources = fileTree("src/main/pegasus").files.collect { it.absolutePath }.toArray(new String[0])
    PegasusDataTemplateGenerator.run(inputPath, defaultPackage, inputPath, false, false, outputPath, sources)
  }
}
compileJava.dependsOn generateRestliBindings
sourceSets.main.java.srcDir generatedRestliCode

def mainClass = "io.papacharlie.gorestli.GoRestliRestSpecParser"

task fatJar(type: Jar) { Jar ->
  manifest {
    attributes "Main-Class": mainClass
  }
  archiveBaseName = "go-restli-spec-parser"
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

group = "io.papacharlie"

javadoc {
  exclude "src/mainGenerated/**"
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "go-restli"
      from components.java

      pom {
        name = "GoRestli spec parser"
        url = "http://github.com/PapaCharlie/go-restli"

        licenses {
          license {
            name = "GNU General Public License v3.0"
            url = "https://github.com/PapaCharlie/go-restli/blob/master/LICENSE"
          }
        }

        developers {
          developer {
            id = "papacharlie"
            name = "Paul Chesnais"
            email = "maven@papacharlie.io"
          }
        }
      }
    }
  }
}
