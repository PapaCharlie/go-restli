plugins {
  id "java-library"
  id "idea"
  id "maven-publish"
}

allprojects {
  repositories {
    mavenLocal()
    maven {
        url "https://linkedin.jfrog.io/artifactory/maven-bintray-temp"
    }
    mavenCentral()
  }
}

dependencies {
  implementation group: "com.linkedin.pegasus", name: "restli-tools", version: pegasusVersion
  implementation group: "com.google.code.gson", name: "gson", version: "2.8.5"
  implementation group: "com.google.guava", name: "guava", version: "28.1-jre"

  testImplementation group: "org.testng", name: "testng", version: "6.14.3"
}

test {
  useTestNG()
}

task fatJar(type: Jar) { Jar ->
  manifest {
    attributes "Main-Class": "io.papacharlie.gorestli.GoRestliRestSpecParser"
  }
  archiveBaseName = "go-restli-spec-parser"
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

group = "io.papacharlie"

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "go-restli"
      from components.java

      pom {
        name = "GoRestli spec parser"
        url = "http://github.com/PapaCharlie/go-restli"

        licenses {
          license {
            name = "GNU General Public License v3.0"
            url = "https://github.com/PapaCharlie/go-restli/blob/master/LICENSE"
          }
        }

        developers {
          developer {
            id = "papacharlie"
            name = "Paul Chesnais"
            email = "maven@papacharlie.io"
          }
        }
      }
    }
  }
}
